plugins {
    id "com.jfrog.bintray" version "1.8.4"
}
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'io.obarch'
version '1.0.4'

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'org.jetbrains:annotations:16.0.2',
            'junit:junit:4.12',
            'com.atlassian.commonmark:commonmark:0.12.1',
            'com.atlassian.commonmark:commonmark-ext-gfm-tables:0.9.0'
    )
    compileOnly(
            'org.mdkt.compiler:InMemoryJavaCompiler:1.3.0'
    )
}

sourceSets {
    test {
        resources {
            srcDirs = ['src/test/java']
        }
    }
}


// === publish to maven central


task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'junit.md'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/qjson/junit.md.git'
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'write junit test in markdown'
            name 'Schema.org Java'
            url 'https://github.com/qjson/junit.md'
            organization {
                name 'org.qjson'
                url 'https://github.com/qjson'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/qjson/junit.md/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/mautini/schemaorg-java/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/qjson/junit.md'
                connection 'scm:git:git://github.com/qjson/junit.md.git'
                developerConnection 'scm:git:ssh://git@github.com:qjson/junit.md.git'
            }
            developers {
                developer {
                    name 'Tao Wen'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

nexusStaging {
    username = ossrhUsername
    password = ossrhPassword
}